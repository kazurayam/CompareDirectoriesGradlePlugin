plugins {
    id "com.kazurayam.compare-directories"
}

ext {
    fixturesDir = "../plugin/src/test/fixtures"
}

// 'compareDirectories' task is registered by the com.kazurayam.compare-directories plugin
compareDirectories {
    dirA = fileTree("${fixturesDir}/A")
    dirB = fileTree("${fixturesDir}/B")
    outputFile = file("build/case1/differences.json")
    diffDir = file("build/case1/diff")
}

// or you can register a task with name you like, which calls the CompareDirectoriesTask
tasks.register('dircomp', com.kazurayam.dircomp.CompareDirectoriesTask) {
    dirA = fileTree("${fixturesDir}/A") { exclude "**/*.png" }
    dirB = fileTree("${fixturesDir}/B") { exclude "**/*.png" }
    outputFile = file("build/case2/differences.json")
    diffDir = file("build/case2/diff")
}

task cleanDircomp(type: com.kazurayam.dircomp.CompareDirectoriesTask) {
    // specify the Inputs and Outputs
    dirA = fileTree("${fixturesDir}/A") { exclude "**/*.png" }
    dirB = fileTree("${fixturesDir}/B") { exclude "**/*.png" }
    outputFile = file("build/case3/differences.json")
    diffDir = file("build/case3/diff")
    // initialize the outputDirectory before the job body
    doFirst {
        println "initializing the outputs"
        println ""
        delete file("build/case3")
        mkdir file("build/case3")
    }
    // do something after the job body
    doLast{
        println ""
        println "Find the result at " + file("build/case3")
    }
}