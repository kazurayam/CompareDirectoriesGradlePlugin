plugins {
    id "com.kazurayam.compare-directories"
}
ext {
    fixturesDir = "../plugin/src/test/fixtures"
}

// 'compareDirectories' task is registered by the com.kazurayam.comare-directories plugin
compareDirectories {
    dirA = layout.projectDirectory.dir("${fixturesDir}/A")
    dirB = layout.projectDirectory.dir("${fixturesDir}/B")
    outputFile = layout.buildDirectory.file("case1/differences.json")
    diffDir = layout.buildDirectory.dir("case1/diff")
}

// or you can register a task with name you like, which calls the CompareDirectoriesTask
tasks.register('dircomp', com.kazurayam.dircomp.CompareDirectoriesTask) {
    dirA = layout.projectDirectory.dir("${fixturesDir}/A")
    dirB = layout.projectDirectory.dir("${fixturesDir}/B")
    outputFile = layout.buildDirectory.file("case2/differences.json")
    diffDir = layout.buildDirectory.dir("case2/diff")
}

task cleanDircomp(type: com.kazurayam.dircomp.CompareDirectoriesTask) {
    // specify the Inputs and Outputs
    dirA = layout.projectDirectory.dir("${fixturesDir}/A")
    dirB = layout.projectDirectory.dir("${fixturesDir}/B")
    outputFile = layout.buildDirectory.file("case3/differences.json")
    diffDir = layout.buildDirectory.dir("case3/diff")
    // initialize the outputDirectory before the job body
    doFirst {
        println "initializing the case3 directory"
        delete layout.buildDirectory.dir("case3")
        mkdir layout.buildDirectory.dir("case3")
    }
    // do something after the job body
    doLast{
        println "done cleanDircomp"
    }
}