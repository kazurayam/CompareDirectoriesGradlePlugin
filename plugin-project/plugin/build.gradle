plugins {
    //id 'java-gradle-plugin'
    id 'groovy'
    id "com.gradle.plugin-publish" version "1.1.0"
}

group = "com.kazurayam"
version = "0.1.7"

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
}

tasks.withType(Test) {
    useJUnitPlatform()
}

def functionalTest = sourceSets.create("functionalTest")
def functionalTestTask = tasks.register("functionalTest", Test) {
    description = "Runs the functional tests."
    group = "verification"
    testClassesDirs = functionalTest.output.classesDirs
    classpath = functionalTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}
tasks.named('check') {
    dependsOn(test, functionalTestTask)
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'
    implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: '4.12'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
    functionalTestImplementation platform(group: 'org.spockframework', name: 'spock-bom', version: '2.3-groovy-3.0')
    functionalTestImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-3.0'
}

gradlePlugin {
    // configure the `java-gradle-plugin` so that it looks at the `sourceSets.functionalTest`
    // to find the tests for the custom plugin
    testSourceSets(sourceSets.functionalTest)
    // This makes `org.gradle.testkit.runner.GradleRunner` class available to the
    // functionalTest classes

    website = 'https://kazurayam.github.io/CompareDirectoriesGradlePlugin/'
    vcsUrl = 'https://github.com/kazurayam/CompareDirectoriesGradlePlugin/'
    plugins {
        compareDirectories {
            id = "com.kazurayam.compareDirectories"
            displayName = "Compare 2 Directories"
            description = "Compares 2 directories to find files as intersection and remainder"
            implementationClass = "com.kazurayam.dircomp.CompareDirectoriesPlugin"
            tags.set(["file-tree"])
        }
    }
}


// How to publish the plugin to the Gradle Plugin Portal?
// $ ./gradlew publishPlugins

// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}