plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id "com.gradle.plugin-publish" version "1.1.0"
}


ext {
    _group = 'com.kazurayam'
    _artifactId = 'CompareDirectories'
    _version = '0.1.5'
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

group = "${_group}"
archivesBaseName = "${_artifactId}"
version = "${_version}"

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Test) {
    useJUnitPlatform()
}

def functionalTest = sourceSets.create("functionalTest")
def functionalTestTask = tasks.register("functionalTest", Test) {
    description = "Runs the functional tests."
    group = "verification"
    testClassesDirs = functionalTest.output.classesDirs
    classpath = functionalTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}
tasks.named('check') {
    dependsOn(test, functionalTestTask)
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'

    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.engine
    functionalTestImplementation platform(libs.spock.bom)
    functionalTestImplementation libs.spock.core
}

gradlePlugin {
    // configure the `java-gradle-plugin` so that it looks at the `sourceSets.functionalTest`
    // to find the tests for the custom plugin
    testSourceSets(sourceSets.functionalTest)
    // This makes `org.gradle.testkit.runner.GradleRunner` class available to the
    // functionalTest classes

    plugins {
        compareDirectories {
            id = "com.kazurayam.compareDirectories"
            displayName = "Compare 2 Directories"
            description = "Compares 2 directories to find files as intersection and remainder"
            implementationClass = "com.kazurayam.dircomp.CompareDirectoriesPlugin"
        }
    }
}

pluginBundle {
    // information displayed in the Gradle Plugin Portal
    website = 'https://kazurayam.github.io/CompareDirectoriesGradlePlugin/'
    vcsUrl = 'https://github.com/kazurayam/CompareDirectoriesGradlePlugin/'
    tags = ["file-tree"]
}

publishing {
    publications {
        maven(MavenPublication) {
            group "${_group}"
            artifactId "${_artifactId}"
            version "${_version}"
            from components.java
        }
    }
    repositories {
        maven {
            name = "gpr"
            url = uri("https://maven.pkg.github.com/kazurayam/CompareDirectoriesGradlePlugin")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }

}


// How to publish the plugin to the Gradle Plugin Portal?
// $ ./gradlew publishPlugins
