plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id "com.gradle.plugin-publish" version "1.1.0"
}

group 'com.kazurayam'
version '0.1.3-SNAPSHOT'

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Test) {
    useJUnitPlatform()
}

def functionalTest = sourceSets.create("functionalTest")
def functionalTestTask = tasks.register("functionalTest", Test) {
    description = "Runs the functional tests."
    group = "verification"
    testClassesDirs = functionalTest.output.classesDirs
    classpath = functionalTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}
tasks.named('check') {
    dependsOn(test, functionalTestTask)
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'

    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.engine
    functionalTestImplementation platform(libs.spock.bom)
    functionalTestImplementation libs.spock.core
}

gradlePlugin {
    // configure the `java-gradle-plugin` so that it looks at the `sourceSets.functinalTest`
    // to find the tests for the custom plugin
    testSourceSets(sourceSets.functionalTest)
    // This makes `org.gradle.testkit.runner.GradleRunner` class available to the
    // functionalTest classes

    plugins {
        directoriesComparator {
            id = "com.kazurayam.directoriesComparator"
            displayName = "Directories Comparator"
            description = "Compares 2 directories to find files as intersection and remainder"
            implementationClass = "com.kazurayam.dircomp.DirectoriesComparatorPlugin"
        }
    }
}

pluginBundle {
    // information displayed in the Gradle Plugin Portal
    website = 'https://kazurayam.github.io/DirectoriesComparatorGradlePlugin/'
    vcsUrl = 'https://github.com/kazurayam/DirectoriesComparatorGradlePlugin/'
    tags = ["file-tree"]
}

publishing {
    repositories {
        maven {
            name = "gpr"
            url = uri("https://maven.pkg.github.com/kazurayam/DirectoriesComparatorGradlePlugin")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
}


// How to publish the plugin to the Gradle Plugin Portal?
// $ ./gradlew publishPlugins
