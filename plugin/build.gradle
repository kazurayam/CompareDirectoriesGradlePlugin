plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version '1.2.1'
    id 'base'
}

group = "com.kazurayam"
version = "0.2.4"

base {
    archivesName = 'compare-directories-plugin'
}

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
    mavenLocal()
}

tasks.withType(Test) {
    useJUnitPlatform()
}

sourceSets {
    functionalTest {
        java {
            srcDir 'src/functionalTest/java'
            srcDir 'src/functionalTest/groovy'
        }
        resources {
            srcDir 'src/functionalTest/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}
tasks.register("functionalTest", Test) {
    description = "Runs the functional tests."
    group = "verification"
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath += sourceSets.functionalTest.runtimeClasspath
}
functionalTest.mustRunAfter test
tasks.named('check') {
    dependsOn(test, functionalTest)
}

dependencies {
    implementation "org.slf4j:slf4j-api:2.0.7"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'
    implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: '4.12'

    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "2.0.7"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
    testImplementation("com.kazurayam:unittest-helper:0.3.3")
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation gradleTestKit()
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
    functionalTestImplementation platform(group: 'org.spockframework', name: 'spock-bom', version: '2.3-groovy-3.0')
    functionalTestImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-3.0'
    functionalTestImplementation("com.kazurayam:unittest-helper:0.3.1")
}

gradlePlugin {
    // configure the `java-gradle-plugin` so that it looks at the `sourceSets.functionalTest`
    // to find the tests for the custom plugin
    testSourceSets(sourceSets.functionalTest)
    // This makes `org.gradle.testkit.runner.GradleRunner` class available to the
    // functionalTest classes

    website = 'https://kazurayam.github.io/CompareDirectoriesGradlePlugin/'
    vcsUrl = 'https://github.com/kazurayam/CompareDirectoriesGradlePlugin/'
    plugins {
        compareDirectories {
            id = 'com.kazurayam.compare-directories'
            displayName = 'Compare 2 directories'
            description = 'Compares 2 directories to find files as intersection and remainder'
            tags.set(['file-tree'])
            implementationClass = 'com.kazurayam.dircomp.CompareDirectoriesPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "compare-directories-plugin"
            from components.java
        }
    }
}

// How to publish the plugin to the Gradle Plugin Portal?
// $ ./gradlew publishPlugins

// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
